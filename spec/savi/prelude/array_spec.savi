:import "spec"

:: A dummy class for use with identity-based equality testing.
:class ArraySpecOpaqueObject
  :new ref new
  :new iso new_iso

:class ArraySpec
  :is Spec
  :const describes: "Array"

  :it "starts with zero space and a null pointer by default"
    data = Array(None).new
    @assert = data.space == 0
    @assert = data.size == 0
    assert data.cpointer.is_null

  :it "allocates the next highest power of two containing the requested space"
    data = Array(None).new(12)
    @assert = data.space == 16
    @assert = data.size == 0
    assert data.cpointer.is_not_null

    data = Array(None).new(16)
    @assert = data.space == 16
    @assert = data.size == 0
    assert data.cpointer.is_not_null

    data = Array(None).new(17)
    @assert = data.space == 32
    @assert = data.size == 0
    assert data.cpointer.is_not_null

  :it "won't reallocate when reserving space within the current allocation"
    data = Array(None).new(12)
    orig_pointer_address = data.cpointer.usize

    data.reserve(16)
    @assert = data.space == 16
    @assert = data.size == 0
    assert data.cpointer.is_not_null
    @assert = data.cpointer.usize == orig_pointer_address

    data.reserve(0)
    @assert = data.space == 16
    @assert = data.size == 0
    assert data.cpointer.is_not_null
    @assert = data.cpointer.usize == orig_pointer_address

  :it "will reallocate when reserving space beyond the current allocation"
    data = Array(None).new(12)
    orig_pointer_address = data.cpointer.usize

    // Go to a much larger size in a different allocator pool,
    // to ensure that we actually get a new pointer address.
    data.reserve(2040)

    @assert = data.space == 2048
    @assert = data.size == 0
    assert data.cpointer.is_not_null
    @assert = data.cpointer.usize != orig_pointer_address

  :it "pushes a new element onto the end of the array and reads them back out"
    array = Array(U64).new << 3 << 6
    @assert = try (array[0]! == 3 | False)
    @assert = try (array[1]! == 6 | False)
    @assert = try (array[1]! = 12, True | False)
    @assert = try (array[1]! == 12 | False)

  :it "can be written as an array literal"
    array = [U8[3], U8[6], U8[12]]
    @assert = array.size == 3
    @assert = try (array[0]! == 3 | False)
    @assert = try (array[1]! == 6 | False)
    @assert = try (array[2]! == 12 | False)
    @assert = try (array[3]!, False | True)

  :it "allows element assignment only within the existing bounds of the array"
    array Array(U8) = [3, 6, 12]
    @assert = try (array[3]!, False | True)
    @assert = try (array[3]! = 36, False | True)

  :it "gives convenient access to the first and last elements, if existent"
    array Array(U8) = [3, 6, 12]
    @assert = try (array.first! == 3 | False)
    @assert = try (array.last! == 12 | False)
    @assert = try (array.first! = 4, True | False)
    @assert = try (array.last! = 11, True | False)
    @assert = try (array.first! == 4 | False)
    @assert = try (array.last! == 11 | False)
    array.clear
    @assert = try (array.first!, False | True)
    @assert = try (array.last!, False | True)

  :it "can be cleared to an empty size"
    array Array(U8) = [3, 6, 12]
    @assert = array.size == 3
    assert array.is_empty.is_false
    assert array.is_not_empty

    array.clear
    @assert = array.size == 0
    assert array.is_empty
    assert array.is_not_empty.is_false
    @assert = try (array[0]!, False | True)

  :it "compares equality for the elements in the array"
    @assert = Array(String).new == Array(String).new
    @assert = ["foo", "bar", "baz"] == ["foo", "bar", "baz"]
    @assert = ["foo", "bar", "baz"] != ["foo", "bar"]
    @assert = ["foo", "bar"] != ["foo", "bar", "baz"]
    @assert = ["foo", "bar", "baz"] != ["baz", "bar", "foo"]
    @assert = ["foo", "bar"] != ["food", "bar"]

  :it "clones itself into a new array"
    array Array(U8) = [3, 6, 12]
    @assert = array.clone == [3, 6, 12]

  :it "cannot clone if the elements aren't aliasable"
    array Array(ArraySpecOpaqueObject'iso) = [
      ArraySpecOpaqueObject.new_iso
      ArraySpecOpaqueObject.new_iso
      ArraySpecOpaqueObject.new_iso
    ]
    @assert = array.clone.size == 0

  :it "returns True if the given element is equal to one already in the array"
    assert ["foo", "bar", "baz"].includes("foo")
    assert ["foo", "bar", "baz"].includes("f").is_false

  :it "replaces via a yield block the element at the given index, if it exists"
    array Array(String) = ["foo", "bar", "baz"]

    assert array.try_replace_at(1) -> (element |
      @assert = element == "bar"
      "BAR"
    )
    assert array.try_replace_at(3) -> (element |
      assert False // assert that this block should never run
      "NOPE"
    ).is_false

    @assert = array == ["foo", "BAR", "baz"]

  :it "returns True if the given element is pointer-identical to one in the array"
    opaque_1 = ArraySpecOpaqueObject.new
    opaque_2 = ArraySpecOpaqueObject.new
    assert [opaque_1].includes(opaque_1)
    assert [opaque_1].includes(opaque_2).is_false

  :it "yields each element in the array, along with the index"
    array_a Array(String) = []
    array_b Array(USize) = []
    ["foo", "bar", "baz"].each_with_index -> (string, index |
      array_a << string
      array_b << index
    )
    @assert = array_a == ["foo", "bar", "baz"]
    @assert = array_b == [0, 1, 2]

  :it "yields each element in the array, in reverse, along with the index"
    array_a Array(String) = []
    array_b Array(USize) = []
    ["foo", "bar", "baz"].reverse_each_with_index -> (string, index |
      array_a << string
      array_b << index
    )
    @assert = array_b == [2, 1, 0]
    @assert =
      array_a == ["baz", "bar", "foo"]

  :it "sorts the elements in the array"
    array Array(U8) = [
      13, 25, 7, 36, 30, 11, 29, 8, 5, 15, 21, 24
      28, 10, 1, 14, 34, 2, 6, 23, 31, 16, 20, 26
      32, 9, 12, 17, 3, 35, 18, 22, 19, 27, 33, 4
    ]

    @assert = array.sort == [
       1,  2,  3,  4,  5,  6,  7,  8,  9, 10, 11, 12
      13, 14, 15, 16, 17, 18, 19, 20, 21, 22, 23, 24
      25, 26, 27, 28, 29, 30, 31, 32, 33, 34, 35, 36
    ]

  :it "cannot sort elements that aren't comparable"
    array Array(ArraySpecOpaqueObject) = [
      ArraySpecOpaqueObject.new
      ArraySpecOpaqueObject.new
      ArraySpecOpaqueObject.new
    ]

    @assert = array.sort == []
