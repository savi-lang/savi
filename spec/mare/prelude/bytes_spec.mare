:import "spec"

:class BytesSpec
  :is Spec
  :const describes: "Bytes"

  :it "starts with zero space and a null pointer by default"
    data = Bytes.new
    @assert = data.space == 0
    @assert = data.size == 0
    @assert = data.cpointer.is_null

  :it "allocates the next highest power of two containing the requested space"
    data = Bytes.new(12)
    @assert = data.space == 16
    @assert = data.size == 0
    @assert = data.cpointer.is_not_null

    data = Bytes.new(16)
    @assert = data.space == 16
    @assert = data.size == 0
    @assert = data.cpointer.is_not_null

    data = Bytes.new(17)
    @assert = data.space == 32
    @assert = data.size == 0
    @assert = data.cpointer.is_not_null

  :it "won't reallocate when reserving space within the current allocation"
    data = Bytes.new(12)
    orig_pointer_address = data.cpointer.usize

    data.reserve(16)
    @assert = data.space == 16
    @assert = data.size == 0
    @assert = data.cpointer.is_not_null
    @assert = data.cpointer.usize == orig_pointer_address

    data.reserve(0)
    @assert = data.space == 16
    @assert = data.size == 0
    @assert = data.cpointer.is_not_null
    @assert = data.cpointer.usize == orig_pointer_address

  :it "will reallocate when reserving space beyond the current allocation"
    data = Bytes.new(12)
    orig_pointer_address = data.cpointer.usize

    // Go to a much larger size in a different allocator pool,
    // to ensure that we actually get a new pointer address.
    data.reserve(2040)

    @assert = data.space == 2048
    @assert = data.size == 0
    @assert = data.cpointer.is_not_null
    @assert = data.cpointer.usize != orig_pointer_address

  :it "can expand size to expose uninitialized bytes within the space available"
    data = Bytes.new(12)
    @assert = data.space == 16
    @assert = data.size == 0

    // We are allowed to expand the size.
    data.resize_possibly_including_uninitialized_memory(10)
    @assert = data.space == 16
    @assert = data.size == 10

    // Contracting to a smaller size is okay too!
    data.resize_possibly_including_uninitialized_memory(8)
    @assert = data.space == 16
    @assert = data.size == 8

    // We aren't allowed to expand beyond the space available.
    data.resize_possibly_including_uninitialized_memory(100)
    @assert = data.space == 16
    @assert = data.size == 16

  :it "converts to and from an array of bytes"
    @assert = b"string".as_array == ['s', 't', 'r', 'i', 'n', 'g']
    @assert = Bytes.from_array(b"string".as_array) == b"string"

  :it "converts to and from a string"
    @assert = b"string".as_string == "string"
    @assert = Bytes.from_string("string") == b"string"

  :it "compares bytewise equality with another bytes string"
    @assert = (b"string" == b"string")
    @assert = (b"string" == b"other").not

  :it "checks if it starts with a substring equal to the other bytes string"
    @assert = b"foo".starts_with(b"foo")
    @assert = b"foo".starts_with(b"food").not
    @assert = b"food".starts_with(b"foo")
    @assert = b"barfood".starts_with(b"foo").not
    @assert = b"barfood".starts_with(b"barf")
    @assert = b"barfood".starts_with(b"")

  :it "checks if it ends with a substring equal to the other bytes string"
    @assert = b"food".ends_with(b"foo").not
    @assert = b"foo".ends_with(b"foo")
    @assert = b"foo".ends_with(b"food").not
    @assert = b"snafoo".ends_with(b"foo")
    @assert = b"snafoozled".ends_with(b"foo").not
    @assert = b"snafoozled".ends_with(b"")

  :it "checks for a common slice with another bytes string at specific offsets"
    @assert = b"foodbar".is_slice_equal(1, b"broodbard", 2, 6)
    @assert = b"foodbar".is_slice_equal(1, b"broodbard", 2, 5)
    @assert = b"foodbar".is_slice_equal(2, b"broodbard", 2, 5).not
    @assert = b"foodbar".is_slice_equal(1, b"broodbard", 2, 7).not
    @assert = b"foodbar".is_slice_equal(1, b"broodbard", 1, 6).not
    @assert = b"foodbar".is_slice_equal(0, b"broodbard", 1, 6).not
    @assert = b"broodbard".is_slice_equal(2, b"foodbar", 1, 6)
    @assert = b"broodbard".is_slice_equal(2, b"foodbar", 1, 5)
    @assert = b"broodbard".is_slice_equal(2, b"foodbar", 2, 5).not
    @assert = b"broodbard".is_slice_equal(2, b"foodbar", 1, 7).not
    @assert = b"broodbard".is_slice_equal(1, b"foodbar", 1, 6).not
    @assert = b"broodbard".is_slice_equal(1, b"foodbar", 0, 6).not

  :it "checks for a common slice with another string at specific offsets"
    @assert = b"foodbar".is_slice_equal(1, "broodbard", 2, 6)
    @assert = b"foodbar".is_slice_equal(1, "broodbard", 2, 5)
    @assert = b"foodbar".is_slice_equal(2, "broodbard", 2, 5).not
    @assert = b"foodbar".is_slice_equal(1, "broodbard", 2, 7).not
    @assert = b"foodbar".is_slice_equal(1, "broodbard", 1, 6).not
    @assert = b"foodbar".is_slice_equal(0, "broodbard", 1, 6).not
    @assert = b"broodbard".is_slice_equal(2, "foodbar", 1, 6)
    @assert = b"broodbard".is_slice_equal(2, "foodbar", 1, 5)
    @assert = b"broodbard".is_slice_equal(2, "foodbar", 2, 5).not
    @assert = b"broodbard".is_slice_equal(2, "foodbar", 1, 7).not
    @assert = b"broodbard".is_slice_equal(1, "foodbar", 1, 6).not
    @assert = b"broodbard".is_slice_equal(1, "foodbar", 0, 6).not

  :it "checks if it is empty or not"
    @assert = b"".is_empty
    @assert = b"".is_not_empty.not
    @assert = b"example".is_empty.not
    @assert = b"example".is_not_empty
    @assert = Bytes.new.is_empty
    @assert = Bytes.new.is_not_empty.not
    @assert = (Bytes.new << b"example").is_empty.not
    @assert = (Bytes.new << b"example").is_not_empty
    @assert = (Bytes.new << b"example").clear.is_empty
    @assert = (Bytes.new << b"example").clear.is_not_empty.not

  :it "clones itself into a new bytes buffer"
    string Bytes = b"example"
    @assert = string.clone == b"example"

  :it "finds the index of another substring within it"
    @assert = try (b"bar food foo".offset_of!(b"foo") == 4 | False)
    @assert = try (b"bar food foo".offset_of!(b"bard"), False | True)
    @assert = try (b"bar food foo".offset_of!(b"nope"), False | True)
    @assert = try (b"bar food foo".offset_of!(b""),     False | True)
    @assert = b"bar food foo".includes(b"foo")
    @assert = b"bar food foo".includes(b"bard").not
    @assert = b"bar food foo".includes(b"nope").not
    @assert = b"bar food foo".includes(b"").not

  :it "hashes the bytes of the buffer"
    @assert = (b"string".hash == 0x4CF51F4A5B5CF110)

  :it "returns the byte at the given byte offset"
    @assert = try (b"example"[3]! == 'm' | False)
    @assert = try (b"example"[7]!, False | True)

  :it "trims bytes off the start and end of the byte string"
    @assert = b"example".trim(2)      == b"ample"
    @assert = b"example".trim(3,  -2) == b"mp"
    @assert = b"example".trim(7)      == b""
    @assert = b"example".trim(5,   4) == b""
    @assert = b"example".trim(-5, -2) == b"amp"
    @assert = b"example".trim(-5,  5) == b"amp"

  :it "trims a mutable byte string in place with the same semantics"
    @assert = b"example".clone.trim_in_place(2)      == b"ample"
    @assert = b"example".clone.trim_in_place(3,  -2) == b"mp"
    @assert = b"example".clone.trim_in_place(7)      == b""
    @assert = b"example".clone.trim_in_place(5,   4) == b""
    @assert = b"example".clone.trim_in_place(-5, -2) == b"amp"
    @assert = b"example".clone.trim_in_place(-5,  5) == b"amp"

  :it "truncates from the left or right"
    @assert = b"example".clone.truncate(5)        == b"examp"
    @assert = b"example".clone.truncate_left(2)   == b"ample"
    @assert = b"example".clone.truncate(0)        == b""
    @assert = b"example".clone.truncate_left(0)   == b"example"
    @assert = b"example".clone.truncate(7)        == b"example"
    @assert = b"example".clone.truncate_left(7)   == b""
    @assert = b"example".clone.truncate(100)      == b"example"
    @assert = b"example".clone.truncate_left(100) == b""

  :it "chops off the left or right"
    center Bytes'ref = b"example".clone
    original_padding = center.space - center.size
    left = center.chop_left(2)
    right = center.chop_right(3)

    @assert = left == b"ex"
    @assert = center == b"amp"
    @assert = right == b"le"

    // The three buffers are adjacent in memory.
    @assert = left.cpointer(2).usize == center.cpointer.usize
    @assert = center.cpointer(3).usize == right.cpointer.usize

    // The first two buffers have a space exactly the same as their size,
    // and the right buffer retains whatever space padding was in the original.
    @assert = left.space == left.size
    @assert = center.space == center.size
    @assert = right.space == right.size + original_padding

    // Any of the three can have a byte appended without bleeding into the
    // adjacent memory of the other buffers, since under the hood, this
    // extension of either the left or center buffer forces reallocation of it.
    left.push('t'), center.push('s'), right.push('l')
    @assert = left == b"ext"
    @assert = center == b"amps"
    @assert = right == b"lel"

    // After the pushes above, The buffers are no longer adjacent in memory,
    // checking with a pointer offset of both the original size and new size.
    @assert = left.cpointer(2).usize != center.cpointer.usize
    @assert = left.cpointer(3).usize != center.cpointer.usize
    @assert = center.cpointer(3).usize != right.cpointer.usize
    @assert = center.cpointer(4).usize != right.cpointer.usize

    // Also now the first two buffers have some padding in their space,
    // since they reserved more space when they were forced to reallocate.
    @assert = left.space > left.size
    @assert = center.space > center.size

    // Chopping with an out-of-bounds offset implicitly uses the full size.
    left_left = left.chop_left(100)
    @assert = left_left == b"ext", @assert = left == b""
    right_right = right.chop_right(100)
    @assert = right_right == b"", @assert = right == b"lel"

  :it "pushes a new byte onto the end of the buffer"
    @assert = Bytes.new.push('o').push('k') == b"ok"

  :it "yields each byte in the buffer"
    bytes Array(U8) = []
    b"foo".each -> (byte | bytes << byte)
    @assert = bytes == ['f', 'o', 'o']

  :it "yields each byte of a subslice of the buffer"
    bytes Array(U8) = []
    b"abcdef".each(1, 5) -> (byte | bytes << byte)
    @assert = bytes == ['b', 'c', 'd', 'e']

  :it "yields each byte, stopping early if the criteria is met"
    bytes Array(U8) = []
    early_stop = b"abcdef".each_until -> (byte | bytes << byte, byte == 'e')
    @assert = bytes == ['a', 'b', 'c', 'd', 'e']
    @assert = early_stop == True

    bytes.clear
    early_stop = b"abcdef".each_until -> (byte | bytes << byte, byte == 'z')
    @assert = bytes == ['a', 'b', 'c', 'd', 'e', 'f']
    @assert = early_stop == False

  :it "yields each byte of a subslice, stopping early if the criteria is met"
    bytes Array(U8) = []
    early_stop = b"abcdef".each_until(1, 5) -> (byte | bytes << byte, byte=='d')
    @assert = bytes == ['b', 'c', 'd']
    @assert = early_stop == True

    bytes.clear
    early_stop = b"abcdef".each_until(1, 5) -> (byte | bytes << byte, byte == 'z')
    @assert = bytes == ['b', 'c', 'd', 'e']
    @assert = early_stop == False

  :it "yields each byte in the buffer, along with the index"
    bytes_a Array(U8) = []
    bytes_b Array(USize) = []
    b"yo".each_with_index -> (byte, index |
      bytes_a << byte
      bytes_b << index
    )
    @assert = bytes_a == ['y', 'o']
    @assert = bytes_b == [0, 1]

  :it "copies bytes from the given buffer onto the end of the byte string"
    @assert = (Bytes.new << b"foo" << b"" << b"bar") == b"foobar"

  :it "creates a multiplicit copy of the bytes sequence"
    @assert = (b"Hey" * 3) == b"HeyHeyHey"
    @assert = (b"Hey" * 1) == b"Hey"
    @assert = (b"Hey" * 0) == b""

  :it "splits the byte string at every occurrence of the given byte value"
    @assert = b"foo.bar.baz".split('.') == [b"foo", b"bar", b"baz"]

  :it "lexically compares the buffer with another buffer of the same length"
    @assert = b"examplE" < b"example"
    @assert = b"example" > b"examplE"
    @assert = (b"example" < b"examplE").not
    @assert = (b"examplE" > b"example").not
    @assert = b"examplE" <= b"example"
    @assert = b"example" >= b"examplE"
    @assert = (b"example" <= b"examplE").not
    @assert = (b"examplE" >= b"example").not

  :it "lexically compares the buffer with an identical buffer"
    @assert = (b"example" < b"example").not
    @assert = (b"example" > b"example").not
    @assert = b"example" <= b"example"
    @assert = b"example" >= b"example"

  :it "lexically compares with a nearly identical buffer of different length"
    @assert = b"example" < b"example!"
    @assert = b"example!" > b"example"
    @assert = (b"example!" < b"example").not
    @assert = (b"example" > b"example!").not
    @assert = b"example" <= b"example!"
    @assert = b"example!" >= b"example"
    @assert = (b"example!" <= b"example").not
    @assert = (b"example" >= b"example!").not

  :it "pushes, reads, and writes native integer types at specific offsets"
    data = Bytes.new

    // Push some values onto the end of the buffer.
    data.push(0xfe)
    data.push_native_u32(0x12345678)
    data.push_native_u64(0xfedcba9876543210)
    @assert = data.size == 13

    // Read the values from the appropriate offsets.
    @assert = try (data[0]! == 0xfe | False)
    @assert = try (data.read_native_u32!(1) == 0x12345678 | False)
    @assert = try (data.read_native_u64!(5) == 0xfedcba9876543210 | False)

    // Reading from an offset too close to the end will raise an error.
    @assert = try (data.read_native_u32!(10), False | True)
    @assert = try (data.read_native_u64!(6), False | True)

    // Now write the values into different offsets (and read them back).
    @assert = try (data.write_native_u64!(1, 0xfedcba9876543210), True | False)
    @assert = try (data.write_native_u32!(9, 0x12345678), True | False)
    @assert = data.size == 13
    @assert = try (data[0]! == 0xfe | False)
    @assert = try (data.read_native_u64!(1) == 0xfedcba9876543210 | False)
    @assert = try (data.read_native_u32!(9) == 0x12345678 | False)

    // Trying to write to a space beyond the buffer size will raise an error.
    @assert = try (data.write_native_u32!(10, 0x12345678), False | True)
    @assert = try (data.write_native_u64!(6, 0xfedcba9876543210), False | True)
