:: A type that can be used as an integer numeric type, signed or unsigned,
:: usually having been declared with a `:numeric` type declaration.
::
:: This is a subtype of the more general `Numeric` trait.
:trait val Integer(T Integer(T)'val)
  :is Numeric(T)
  :is Integer.Representable
  :is Integer.Arithmetic(T)
  :is Integer.SafeArithmetic(T)
  :is Integer.WideArithmetic(T)
  :is Integer.BitwiseArithmetic(T)
  :is Numeric.Comparable(T)
  :is Integer.Countable(T)
  :is Integer.Formattable(T)

:: A type which conveys information about the machine-level representation
:: of an integer numeric type, including the standard information about
:: general numeric types, as well as integer-specific information.
:trait val Integer.Representable
  :is Numeric.Representable

  :: The number of bits representing the magnitude.
  :: For unsigned integers, this is the entire `bit_width`.
  :: For signed integers, this is one less than the `bit_width`.
  :fun non mag_bit_width U8

:: A type which can do arithmetic operations of the given integer type T,
:: each operation producing a result of that same type T.
::
:: This trait extends the methods available in genal `Numeric.Arithmetic`
:: to add more methods, which are integer-specific.
:trait val Integer.Arithmetic(T Integer(T)'val)
  :is Numeric.Arithmetic(T)

  :: Get the next power of 2 that is greater than or equal to this value.
  ::
  :: This is often used in allocation algorithms that expect to use
  :: powers of two for the allocation sizes.
  :fun val next_pow2 T: compiler intrinsic

:: A type which can do "safe" arithmetic operations of the given integer type T,
:: each operation producing a result of that same type T, or raising an error.
::
:: A "safe" operation is one that raises an error in situations where the
:: corresponding `Numeric.Arithmetic` method would produce a result that
:: deviates from the normal mathematical theory for that operation, such as
:: in the case of overflow/underflow of the type's bounds or division by zero.
::
:: Using these operations may be slightly more costly, and forces the caller
:: to explicitly deal with the error case using normal error raising flow.
:: But it is idiomatic to prefer using these "safe" operations at call sites
:: where handling the error case is important, rather than checking
:: for safety pre-conditions using other means at the call site.
:trait val Integer.SafeArithmetic(T Integer(T)'val)
  :: Add this value to another value, resulting in a sum value.
  :: Raises an error if the proper result would overflow the `max_value`
  :: (or in a signed integer type, when adding a negative number,
  :: if the proper result would underflow the `min_value`).
  ::
  :: Use the `+` method instead if wrap-around overflow semantics are desired.
  :: Use the `saturating_add` method instead if returning the `max_value`
  :: on overflow or the `min_value` on underflow is the desired behavior.
  :fun val "+!"(other T) T

  :: Subtract another value from this value, resulting in a difference value.
  :: Raises an error if the proper result would underflow the `min_value`
  :: (or in a signed integer type, when subtracting a negative number,
  :: if the proper result would overflow the `max_value`).
  ::
  :: Use the `-` method instead if wrap-around overflow semantics are desired.
  :: Use the `saturating_subtract` method instead if returning the `max_value`
  :: on overflow or the `min_value` on underflow is the desired behavior.
  :fun val "-!"(other T) T

  :: Multiply this value with another value, resulting in a product value.
  :: Raises an error if the proper result would overflow the `max_value`
  :: (or in a signed integer type, when one of the factors is a negative number,
  :: if the proper result would underflow the `min_value`).
  ::
  :: Use the `*` method instead if wrap-around overflow semantics are desired.
  :: Use the `saturating_multiply` method instead if returning the `max_value`
  :: on overflow or the `min_value` on underflow is the desired behavior.
  :fun val "*!"(other T) T

  :: Add this value to another value, resulting in a sum value.
  :: Returns `max_value` if the proper result would be greater than `max_value`.
  :: Returns `min_value` if the proper result would be less than `min_value`,
  :: which is possible for signed integer types when adding a negative.
  ::
  :: Use the `+!` method instead if raising an error on these cases is desired.
  :: Use the `+` method instead if wrap-around overflow semantics are desired.
  :fun val "saturating_add"(other T) T

  :: Subtract another value from this value, resulting in a difference value.
  :: Returns `min_value` if the proper result would be less than `min_value`.
  :: Returns `max_value` if the proper result would be greater than `max_value`,
  :: which is possible for signed integer types when subtracting a negative.
  ::
  :: Use the `-!` method instead if raising an error on these cases is desired.
  :: Use the `-` method instead if wrap-around overflow semantics are desired.
  :fun val "saturating_subtract"(other T) T

  :: Multiply this value with another value, resulting in a product value.
  :: Returns `max_value` if the proper result would be greater than `max_value`.
  :: Returns `min_value` if the proper result would be less than `min_value`,
  :: which is possible for signed integer types when one factor is negative.
  ::
  :: Use the `*!` method instead if raising an error on these cases is desired.
  :: Use the `*` method instead if wrap-around overflow semantics are desired.
  :fun val "saturating_multiply"(other T) T

:: A type which can do "wide" arithmetic operations of the given integer type T,
:: allowing integer arithmetic that is guaranteed to neither lose information
:: nor abort the operation by raising an error.
::
:: These operations are meant to sometimes replace the use of those in
:: `Integer.Arithmetic` when it is desired to not lose information.
:: Or alternatively, they replace `Integer.SafeArithmetic` when it is desired
:: to successfully complete operations without error even when they overflow.
:trait val Integer.WideArithmetic(T Integer(T)'val)
  // TODO: :fun val wide_add(other T) Pair(Bool, T)
  // TODO: :fun val wide_subtract(other T) Pair(Bool, T)
  // TODO: :fun val wide_divide(other T) Pair(T, T)

  :: Multiply avoiding overflow, returning a pair where the first element is
  :: the most significant bits, and the second is the least significant bits
  :: (which is the same modulo max size minus one value that would be returned
  :: from the wrap-around overflow semantics of normal integer multiplication).
  :fun val wide_multiply(other T) Pair(T, T)

:: A type which can do bitwise arithmetic operations of the given type T,
:: each operation producing a result of that same type T (or sometimes
:: accepting or producing `U8` specifically when counting a number of bits).
:trait val Integer.BitwiseArithmetic(T Integer(T)'val)
  :: Invert the value, producing a value whose every bit is the inverse of the
  :: bit that was in the corresponding position in the original value.
  :fun val invert T

  :: Combine, using a bitwise "and", this value and the other value.
  ::
  :: The result's bits will be set to one in positions where both
  :: values had a one bit, and will be set to zero in all other positions.
  :fun val bit_and(other T) T

  :: Combine, using a bitwise "or", this value and the other value.
  ::
  :: The result's bits will be set to one in positions where either or both
  :: values had a one bit, and will be set to zero in all other positions.
  :fun val bit_or(other T) T

  :: Combine, using a bitwise "exclusive or", this value and the other value.
  ::
  :: The result's bits will be set to one in positions where just one of the two
  :: values had a one bit, and will be set to zero in all other positions.
  :fun val bit_xor(other T) T

  :: Do a bitwise "shift left" on this value by the given number of bits.
  ::
  :: Each bit will move the given number of bit positions "leftward".
  :: That is, each bit will become "more significant" than it was, with the
  :: highest bits shifting out of the bounds of the `bit_width` to disappear,
  :: and the lowest bits being filled by zeroes in the empty space left behind.
  ::
  :: If more than `bit_width` bits are shifted, the result is all zero bits.
  ::
  :: Because each bit represents a successive power of two, this operation is
  :: equivalent to multiplying the value by 2 the given number of times.
  :fun val bit_shl(bits U8) T

  :: Do a bitwise "shift right" on this value by the given number of bits.
  ::
  :: Each bit will move the given number of bit positions "rightward".
  :: That is, each bit will become "less significant" than it was, with the
  :: lowest bits shifting out of the bounds of the `bit_width` to disappear,
  :: and the highest bits being filled by zeroes in the empty space left behind.
  ::
  :: If more than `bit_width` bits are shifted, the result is all zero bits.
  ::
  :: Because each bit represents a successive power of two, this operation is
  :: equivalent to dividing the value by 2 the given number of times, provided
  :: that the value is a positive integer rather than a negative integer
  :: (as a shifted negative integer will have its sign bit filled with a zero).
  :fun val bit_shr(bits U8) T

  :: Do a bitwise "rotate left" on this value by the given number of bits.
  ::
  :: Each bit will move the given number of bit positions "leftward".
  :: That is, each bit will become "more significant" than it was, with the
  :: highest bits shifting out of the bounds of the `bit_width` and wrapping
  :: around into the space left behind by the movement of the lowest bits.
  ::
  :: Unlike `bit_shl`, this operation does not lose information from the bits,
  :: because the bits shifted off the left side appear again on the right side.
  :fun val bit_rotl(bits U8) T

  :: Do a bitwise "rotate right" on this value by the given number of bits.
  ::
  :: Each bit will move the given number of bit positions "rightward".
  :: That is, each bit will become "less significant" than it was, with the
  :: lowest bits shifting out of the bounds of the `bit_width` and wrapping
  :: around into the space left behind by the movement of the highest bits.
  ::
  :: Unlike `bit_shr`, this operation does not lose information from the bits,
  :: because the bits shifted off the right side appear again on the left side.
  :fun val bit_rotr(bits U8) T

  :: Return true if the value is even (its least significant bit is unset).
  :fun val is_even Bool

  :: Return true if the value is odd (its least significant bit is set).
  :fun val is_odd Bool

  :: Count significant bits, starting with the most significant nonzero bit,
  :: until the least significant bit (regardless of whether it is zero).
  :fun val significant_bits U8

  :: Count consecutive zero bits, starting with the most significant bit,
  :: until the first one bit is reached (or until the end of the bit sequence).
  :fun val leading_zero_bits U8

  :: Count consecutive zero bits, starting with the least significant bit,
  :: until the first one bit is reached (or until the end of the bit sequence).
  :fun val trailing_zero_bits U8

  :: Count the total number of one bits in the value's bitwise representation.
  :fun val total_one_bits U8

  :: Count the total number of zero bits in the value's bitwise representation.
  :fun val total_zero_bits U8: @invert.total_one_bits

  :: Reverse the order of the bits in the value, producing a new value in which
  :: the most significant bits have become least significant, and vice versa.
  :fun val reverse_bits T

  :: Reverse the order of the bytes in the value, but keeping the internal
  :: order of bits within each byte intact.
  ::
  :: This is commonly used for converting big-endian values to little-endian,
  :: or vice versa. That is, if you have a value in its native-endian format
  :: for your platform, you can use this method to convert it to the opposite
  :: sequence of bytes, for encoding to/from another plaform's expectations.
  :: To do so conditionally based on the current plafrom, you can make use
  :: of the `native_to_be`, `native_to_le`, `be_to_native`, and `le_to_native`
  :: convenience functions which are intended for this purpose.
  :fun val swap_bytes T

  :: Convert the native-endian representation of a value to big-endian.
  :: If the platform is natively big-endian, the value will return unchanged.
  :: If the platform is natively little-endian, the byte order will be swapped.
  :fun val native_to_be T

  :: Convert the native-endian representation of a value to little-endian.
  :: If the platform is natively little-endian, the value will return unchanged.
  :: If the platform is natively big-endian, the byte order will be swapped.
  :fun val native_to_le T

  :: Convert the big-endian representation of a value to the native-endian.
  :: If the platform is natively big-endian, the value will return unchanged.
  :: If the platform is natively little-endian, the byte order will be swapped.
  :fun val be_to_native T

  :: Convert the little-endian representation of a value to the native-endian.
  :: If the platform is natively little-endian, the value will return unchanged.
  :: If the platform is natively big-endian, the byte order will be swapped.
  :fun val le_to_native T

  :: Convert the native-endian representation of a value to network byte order
  :: (big-endian).
  :: Alias of `native_to_be`.
  :fun val native_to_network T
    @native_to_be

  :: Convert the network byte-order representation (bid-endian) of a value to
  :: the native-endian.
  :: Alias of `be_to_native`.
  :fun val network_to_native T
    @be_to_native

:: A type which can be counted, using the given type T.
:trait val Integer.Countable(T Integer(T)'val)
  :is Numeric.Representable

  :: Yield each successive integer that is less than the given limit.
  :: For example, `3.times` will yield 0, 1, and 2, effectively
  :: running the given yield block 3 times.
  :fun val times T
    :yields T for None

:: A type which can be formatted as the given integer type T.
:trait val Integer.Formattable(T Integer(T)'val)
  :fun as_val T

  :fun format: Integer.Format(T)._new(@as_val)

  :is IntoString
  :fun into_string(out String'ref): @format.decimal.into_string(out)
  :fun into_string_space: @format.decimal.into_string_space

:: This trait isn't meant to be used externally. It's just a base implementation
:: of methods to be copied into every new integer `:numeric` type.
:trait val Integer.BaseImplementation // TODO: don't use :trait for this... `common`?
  :: An internal method for converting a `box` number value to a `val` one.
  :: This is sometimes needed for `:fun box` uses of `val` methods.
  :: It is safe because no builtin numeric type can have interior mutability.
  :: TODO: Remove this hack in favor of a more generalized mechanism
  :: for marking types which have no possibility of interior mutability,
  :: allowing them to treat any `box` reference as a `val` reference.
  :fun as_val @'val: compiler intrinsic

  :is Integer.Representable
  :fun non mag_bit_width U8
    if @is_signed (@bit_width - 1 | @bit_width)

  :is Numeric.Convertible

  :is Numeric.Comparable(@)

  :is Numeric.Bounded(@)
  :fun non zero @'val: compiler intrinsic
  :fun non one @'val: compiler intrinsic
  :fun non min_value @'val: compiler intrinsic
  :fun non max_value @'val: compiler intrinsic

  :is Integer.Arithmetic(@)
  :fun val next_pow2 @: compiler intrinsic

  :is Integer.SafeArithmetic(@)
  :fun val "+!"(other @) @: compiler intrinsic
  :fun val "-!"(other @) @: compiler intrinsic
  :fun val "*!"(other @) @: compiler intrinsic
  :fun val saturating_add(other @) @: compiler intrinsic
  :fun val saturating_subtract(other @) @: compiler intrinsic
  :fun val saturating_multiply(other @) @
    // There is no LLVM intrinsic for saturating multiplication of integers,
    // so we implement it using partial multiplication here below.
    try (
      @ *! other
    |
      left_is_negative = @ < @zero
      right_is_negative = other < @zero
      one_is_negative = left_is_negative != right_is_negative
      if one_is_negative (@min_value | @max_value)
    )

  :is Integer.WideArithmetic(@)
  :fun val wide_multiply(other @) Pair(@, @): compiler intrinsic

  :is Integer.BitwiseArithmetic(@)
  :fun val invert @: compiler intrinsic
  :fun val bit_and(other @) @: compiler intrinsic
  :fun val bit_or(other @) @: compiler intrinsic
  :fun val bit_xor(other @) @: compiler intrinsic
  :fun val bit_shl(bits U8) @: compiler intrinsic
  :fun val bit_shr(bits U8) @: compiler intrinsic
  :fun val bit_rotl(bits U8) @
    @bit_shl(bits).bit_or(
      @bit_shr(@bit_width - bits)
    )
  :fun val bit_rotr(bits U8) @
    @bit_shr(bits).bit_or(
      @bit_shl(@bit_width - bits)
    )
  :fun val is_even Bool: @bit_and(@one) == @zero
  :fun val is_odd Bool: @bit_and(@one) == @one
  :fun val significant_bits U8: @bit_width - @leading_zero_bits
  :fun val leading_zero_bits U8: compiler intrinsic
  :fun val trailing_zero_bits U8: compiler intrinsic
  :fun val total_one_bits U8: compiler intrinsic
  :fun val reverse_bits @: compiler intrinsic
  :fun val swap_bytes @: compiler intrinsic
  :fun val native_to_be @: if Platform.is_big_endian (@ | @swap_bytes)
  :fun val native_to_le @: if Platform.is_little_endian (@ | @swap_bytes)
  :fun val be_to_native @: if Platform.is_big_endian (@ | @swap_bytes)
  :fun val le_to_native @: if Platform.is_little_endian (@ | @swap_bytes)

  :is Integer.Countable(@)
  :fun val times
    i = @zero
    while (i < @) (
      yield i
      i = i + @one
    )
    @

  :is Integer.Formattable(@'val)

  :is TraceData
  :fun trace_data(trace TraceData.Observer)
    if @is_signed (
      case @bit_width == (
      | 64 | trace.primitive_i64(@i64)
      | 32 | trace.primitive_i32(@i32)
      | 16 | trace.primitive_i16(@i16)
      |  8 | trace.primitive_i8(@i8)
      )
    |
      case @bit_width == (
      | 64 | trace.primitive_u64(@u64)
      | 32 | trace.primitive_u32(@u32)
      | 16 | trace.primitive_u16(@u16)
      |  8 | trace.primitive_u8(@u8)
      )
    )

  // TODO: explicit conformance to a particular trait for `hash`
  :fun hash USize
    if (USize.bit_width == 32) (
      x = @usize
      x = x.invert + x.bit_shl(15)
      x = x.bit_xor(x.bit_shr(12))
      x = x + x.bit_shl(2)
      x = x.bit_xor(x.bit_shr(4))
      x = (x + x.bit_shl(3)) + x.bit_shl(11)
      x = x.bit_xor(x.bit_shr(16))
      x
    |
      @hash64.usize
    )

  // TODO: explicit conformance to a particular trait for `hash64`
  :fun hash64 U64
    x = @u64
    x = x.invert + x.bit_shl(21)
    x = x.bit_xor(x.bit_shr(24))
    x = (x + x.bit_shl(3)) + x.bit_shl(8)
    x = x.bit_xor(x.bit_shr(14))
    x = (x + x.bit_shl(2)) + x.bit_shl(4)
    x = x.bit_xor(x.bit_shr(28))
    x = x + x.bit_shl(31)
    x

:: This trait isn't meant to be used externally. It's just a base implementation
:: of methods to be copied into every new integer `:enum` type.
:trait val Integer.BaseImplementation.Enum // TODO: don't use :trait for this... `common`?
  :copies Integer.BaseImplementation

  :const bit_width U8: 8
  :const byte_width U8: compiler intrinsic

  :fun non from_u64!(value U64) @'val
  :fun member_name String

  :is IntoString
  :fun into_string(out String'ref): @member_name.into_string(out)
  :fun into_string_space: @member_name.into_string_space

  :is TraceData
  :fun trace_data(trace TraceData.Observer)
    trace.primitive_name(@member_name)
