:primitive _MapEmpty
:primitive _MapDeleted

:class KV (K, V) // TODO: use a tuple literal instead
  :prop key K
  :prop value V
  :new (@key, @value)

:class Map (K, V, H HashFunction(K))
  :prop _size USize: 0
  :prop _array Array((KV(K, V) | _MapEmpty | _MapDeleted))
  
  :fun size: @_size
  
  :new (prealloc USize = 6)
    @_init_array(((prealloc * 4) / 3).max(8).next_pow2)
  
  :fun ref _init_array (space USize)
    @_array = Array((KV(K, V) | _MapEmpty | _MapDeleted)).new(space)
    while (space > 0) ( // TODO: space.times ...
      @_array << _MapEmpty
    space = space - 1)
  
  :fun ref _resize (space USize)
    space = space.max(@_size) // only grow the array - never shrink it
    old_array = @_array
    
    // TODO: would we get any perf benefit from resizing the array instead?
    @_init_array(space)
    
    old_array.each -> (entry |
      if (entry <: KV(K, V)) (
        try (@_array[@_search(entry.key)]! = entry)
      )
    )
    @
  
  :: Remove all keys and values from the map.
  :fun ref clear
    @_size = 0
    @_init_array(8) // the default prealloc of 6 corresponds to an array of 8
    @
  
  :: Return the value assigned to the given key, if present.
  :: Raises an error if the key is not present.
  ::
  :: $ map = Map(String, U64).new
  :: $ map["example"]!
  :: > error! // this key hasn't been assigned a value yet
  :: $ map["example"] = 99, map["example"]!
  :: > 99
  :: $ map.delete("example"), map["example"]!
  :: > error! // this key no longer has an assigned value
  
  :fun "[]!" (key K)
    @_array[@_search(key)]!.as!(KV(K, V)).value
  
  :: Assign the given value to the given key in the map.
  :: The previous value will be overwritten and discarded if present.
  ::
  :: $ map = Map(String, U64).new
  :: $ map["example"] = 99, map["example"]!
  :: > 99
  :: $ map["example"] = 33, map["example"]!
  :: > 33
  
  :fun ref "[]=" (key K, value V)
    value_alias = value
    try (
      index = @_search(key)
      entry = @_array[index]!
      
      case (
      | entry <: KV(K, V) | entry.value = --value
      |
        @_array[index]! = KV(K, V).new(--key, --value)
        @_size = @_size + 1 // TODO: @_size += 1
        
        if (entry <: _MapEmpty) (
          if (@_size * 4 > @_array.size * 3) (
            @_resize(@_array.size * 2)
          )
        )
      )
    )
    value_alias
  
  :: Discard the value assigned to the given key.
  ::
  :: $ map = Map(String, U64).new
  :: $ map["example"] = 99, map["example"]!
  :: > 99
  :: $ map.delete("example"), map["example"]!
  :: > error! // this key no longer has an assigned value
  :: $ map.delete("example") // deleting it again has no effect
  
  :fun ref delete (key K)
    try (
      @_array[@_search(key)]! = _MapDeleted
      @_size = @_size - 1 // TODO: @_size -= 1
    )
    None
  
  :: Yield each key and value in the map.
  :fun each
    @_array.each -> (entry |
      if (entry <: KV(K, V)) (
        yield (entry.key, entry.value)
      )
    )
    @
  
  :: Yield each key in the map.
  :fun each_key
    @_array.each -> (entry |
      if (entry <: KV(K, V)) (
        yield entry.key
      )
    )
    @
  
  :: Yield each value in the map.
  :fun each_value
    @_array.each -> (entry |
      if (entry <: KV(K, V)) (
        yield entry.value
      )
    )
    @
  
  :fun _search (key)
    hash = H.hash(key).usize()
    
    idx_del = @_array.size
    mask = idx_del - 1
    idx = hash.bit_and(mask)
    
    result_idx = idx_del
    found = False
    
    try (
      @_array.each_with_index -> (entry, i |
        entry = @_array[idx]!
        
        case (
        | entry <: KV(K, V) |
          if H.equal(key, entry.key) (
            result_idx = idx
            found = True
            error! // TODO: use early return instead of error!
          )
        | entry <: _MapEmpty |
          if (idx_del > mask) (
            result_idx = idx
          |
            result_idx = idx_del
          )
          found = False
          error!
        | entry <: _MapDeleted |
          if (idx_del > mask) (
            idx_del = idx
          )
        )
        
        idx = (hash + ((i + (i * i)) / 2)).bit_and(mask)
      )
    )
    
    result_idx // TODO: also return `found`
