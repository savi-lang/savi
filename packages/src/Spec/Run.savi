:trait val Spec.Run.Any
  :fun val run(process Spec.Process) None // TODO: fun box instead of fun val

:class val Spec.Run(A Spec)
  :is Spec.Run.Any
  :let env Env
  :new (@env)

  // TODO: avoid this indirection
  :fun _new_spec(process) A
    A.new(Spec.Context.new(@env, process, A.describes, ""))

  :: Run all spec features for the Spec type indicated by the type argument,
  :: using the given Env to construct an instance of that Spec type.
  ::
  :: A summary of successes and failures will be printed to `env.out`.
  :: If there are one or more failures, `env.exit_code` will be set accordingly.
  :fun val run(process Spec.Process) None // TODO: fun box instead of fun val
    type ReflectionOfType(A) = reflection_of_type @_new_spec(process)
    features = type.features.select -> (feature |
      feature.tags.includes("it")
    )

    process.spec_began(A.describes, features.size)

    features.each -> (feature |
      SpecExampleRunner(A).new(@env, process, feature)
    )

:actor SpecExampleRunner(A Spec)
  :let example ReflectionFeatureOfType(A)

  :new (env, process, @example)
    helper = Spec.Context.new(env, process, A.describes, @example.name)
    spec = A.new(helper)

    process.example_began(A.describes, @example.name)

    called = @example.maybe_call_mutator(spec)
    if called.not (
      _Log.bug(env, "failed to call a spec feature")
    )

    process.example_close(A.describes, @example.name)
