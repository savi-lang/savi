:import "collections" (OrderedMap, HashEq)

:actor Specs
  :prop env Env
  :prop reporter SpecReporter
  :prop spec_count USize
  :prop statuses: OrderedMap(String, SpecStatus).new

  :new (@env, @spec_count)
    @reporter = SpecReporterLinear.new(@env, @statuses, SpecReporterFull.new(@env))

  :fun non run (env, runners Array(SpecRunAny)) // TODO: shouldn't need this indirection
    specs = @new(env, runners.size)
    runners.each -> (runner | runner.run(specs))

  :fun non _fail (env Env)
    env.exit_code = 1 // Mark the entire process as a failure.

  :fun non _bug (env Env, text)
    env.err.write("You've encountered a bug in the spec library: ")
    env.err.print(text)
    Specs._fail(env)

  :be spec_began (spec String, expected_examples USize)
    // TODO: complain if spec is empty or duplicate
    status = SpecStatus.new(spec)
    status.expected_examples = expected_examples
    @statuses[spec] = status
    @reporter.spec_began(spec)

  :fun ref spec_ended (spec String)
    try (
      status = @statuses[spec]!
      status.ended = True
      status.examples.each -> (example, example_status |
        if (example_status.ended.not) (
          Specs._bug(@env, "spec_ended before all of its examples ended")
        )
      )
      @reporter.spec_ended(spec)
    |
      Specs._bug(@env, "spec_ended before the spec_began")
    )

  :be example_began (spec String, example String):
    // TODO: complain if example is empty or duplicate
    try (
      @statuses[spec]!.examples[example] = SpecExampleStatus.new(example)
      @reporter.example_began(spec, example)
    |
      Specs._bug(@env, "example_began before the spec_began")
    )

  :be example_close (spec String, example String): @example_ended(spec, example)
  :fun ref example_ended (spec String, example String)
    try (
      status = @statuses[spec]!
      example_status = status.examples[example]!
      example_status.ended = True

      if (
        status.examples.size >= status.expected_examples
        && status.examples.has_all -> (_, es | es.ended)
      ) (
        @spec_ended(spec)
      )

      @reporter.example_ended(spec, example)
    |
      Specs._bug(@env, "example_ended before the example_began")
    )

  :be assert (spec String, example String, success Bool, pos SourceCodePosition)
    event = SpecEventAssert.new(success, pos)

    // If it was a failed assertion, mark the entire process as a failure.
    if success.not Specs._fail(@env)

    try (
      @statuses[spec]!.examples[example]!.events << event
      @reporter.event(spec, example, event)
    |
      Specs._bug(@env, "assert before the example_began and/or spec_began")
    )
