:import "OSError" // TODO: can this be removed?
:import "bytes" (ByteStreamReader, ByteStreamChunkedWriter)
:import "io"

:class TCPConnectionEngine
  :is IOEngine(IOAction)
  :var io: IOCoreEngine.new
  :var listen (TCPListener | None): None
  :var connect_error OSError: OSError.None
  :let read_stream: ByteStreamReader.new
  :let write_stream: ByteStreamChunkedWriter.new

  :fun non connect(
    // TODO: TCPConnectionAuth, rather than ambient authority.
    actor AsioEventNotify
    host String
    service String
    from String = ""
  )
    try (
      @_new_with_io(IOCoreEngine.new_tcp_connect!(actor, host, service, from))
    |
      invalid = @_new_with_io(IOCoreEngine.new)
      invalid.connect_error = OSError.EINVAL
      invalid
    )

  :fun non accept(
    actor AsioEventNotify
    listen TCPListener
    ticket TCPAcceptTicket
  )
    io = IOCoreEngine.new_from_fd_rw(actor, ticket._fd)
    new = @_new_with_io(io)
    new.listen = listen
    new

  :new _new_with_io(@io)

  :fun ref react(event CPointer(AsioEvent), flags U32, arg U32) @
    :yields IOAction
    @io.react(event, flags, arg) -> (action |
      case action == (
      | IOActionClosed |
        try @listen.as!(TCPListener)._conn_closed

        // TODO: windows complete writes, flush-after-mute (pending writes logic from Pony)
        // | IOActionWrite |
        //   ...
      )
      yield action
    )
    @

  :fun ref close
    @io.close
    @

  :fun ref flush
    @write_stream.flush(@io)

  :fun ref pending_reads
    :yields USize for None
    if Platform.windows (
      None // TODO: @_windows_complete_reads(arg)
    |
      @_pending_reads_unix -> (bytes_available | yield bytes_available)
    )
    @

  :fun ref _pending_reads_unix None
    :yields USize for None
    while @io.is_readable (
      try (
        bytes_read = @read_stream.receive_from!(@io)
        if (bytes_read > 0) (yield @read_stream.bytes_ahead_of_marker)
      )
    )
