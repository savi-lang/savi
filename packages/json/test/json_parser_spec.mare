:import "spec"

:class JsonParserSpec
  :is Spec
  :const describes: "JsonParser"

  :it "parses keywords"
    @assert = @example("null", <<<
      [JsonTokenNull, 0, 4, 0, 0.0, ""]
    >>>)
    @assert = @example("true", <<<
      [JsonTokenTrue, 0, 4, 0, 0.0, ""]
    >>>)
    @assert = @example("false", <<<
      [JsonTokenFalse, 0, 5, 0, 0.0, ""]
    >>>)
    @assert = @example(" \t\nnull\n\t ", <<<
      [JsonTokenNull, 3, 7, 0, 0.0, ""]
    >>>)

    @assert = @example_error("nugget",    "nu")
    @assert = @example_error("truth",     "tru")
    @assert = @example_error("falsehood", "false")
    @assert = @example_error("bogus",     "")

  :it "parses numbers"
    @assert = @example("123", <<<
      [JsonTokenNumberPre, 0, 0, 0, 0.0, ""]
      [JsonTokenNumber, 0, 3, 123, 0.0, ""]
    >>>)
    @assert = @example("-123", <<<
      [JsonTokenNumberPre, 0, 0, 0, 0.0, ""]
      [JsonTokenNumber, 0, 4, -123, 0.0, ""]
    >>>)
    @assert = @example("123.456", <<<
      [JsonTokenNumberPre, 0, 0, 0, 0.0, ""]
      [JsonTokenNumber, 0, 7, 0, 123.456, ""]
    >>>)
    @assert = @example("-123.456", <<<
      [JsonTokenNumberPre, 0, 0, 0, 0.0, ""]
      [JsonTokenNumber, 0, 8, 0, -123.456, ""]
    >>>)
    @assert = @example("123e2", <<<
      [JsonTokenNumberPre, 0, 0, 0, 0.0, ""]
      [JsonTokenNumber, 0, 5, 0, 12300, ""]
    >>>)
    @assert = @example("-123e-2", <<<
      [JsonTokenNumberPre, 0, 0, 0, 0.0, ""]
      [JsonTokenNumber, 0, 7, 0, -1.23, ""]
    >>>)
    @assert = @example("-123.456e2", <<<
      [JsonTokenNumberPre, 0, 0, 0, 0.0, ""]
      [JsonTokenNumber, 0, 10, 0, -12345.6, ""]
    >>>)
    @assert = @example("-123.4e-2", <<<
      [JsonTokenNumberPre, 0, 0, 0, 0.0, ""]
      [JsonTokenNumber, 0, 9, 0, -1.234, ""]
    >>>)

    @assert = @example_error("---",             "-")
    @assert = @example_error("123456789ABCDEF", "123456789")
    @assert = @example_error("0x0",             "0")
    @assert = @example_error("123...",          "123.")
    @assert = @example_error("123.",            "123.")
    @assert = @example_error("127.0.0.1",       "127.0")
    @assert = @example_error("5eed",            "5e")
    @assert = @example_error("5e---",           "5e-")
    @assert = @example_error("5e",              "5e")
    @assert = @example_error("1.2e3.4",         "1.2e3")
    @assert = @example_error(" 1 2 3 ",         " 1 ")

  :it "parses strings"
    @assert = @example(<<<"apple">>>, <<<
      [JsonTokenStringPre, 1, 1, 0, 0.0, ""]
      [JsonTokenString, 1, 6, 0, 0.0, "apple"]
    >>>)
    @assert = @example("                    \"apple\"   ", <<<
      [JsonTokenStringPre, 21, 21, 0, 0.0, ""]
      [JsonTokenString, 21, 26, 0, 0.0, "apple"]
    >>>)
    @assert = @example("                    \"\"   ", <<<
      [JsonTokenStringPre, 21, 21, 0, 0.0, ""]
      [JsonTokenString, 21, 21, 0, 0.0, ""]
    >>>)

    @assert = @example_error(" \" ",                       " \" ")
    @assert = @example_error(" \"apple",                   " \"apple")
    @assert = @example_error(" \"\\\"",                    " \"\\\"")
    @assert = @example_error(" \"\\urge\" ",               " \"\\u")
    @assert = @example_error(" \"\\u123\" ",               " \"\\u123")
    @assert = @example_error(" \"\\ud800\\n\" ",           " \"\\ud800\\")
    @assert = @example_error(" \"apple \\ud800 banana\" ", " \"apple \\ud800")
    @assert = @example_error(" \"\\ud800\\ud800\" ",       " \"\\ud800\\u")

  :it "parses arrays"
    @assert = @example("[]", <<<
      [JsonTokenArrayStart, 0, 1, 0, 0.0, ""]
      [JsonTokenArrayEnd, 1, 2, 0, 0.0, ""]
    >>>)
    @assert = @example("  [  ]  ", <<<
      [JsonTokenArrayStart, 2, 3, 0, 0.0, ""]
      [JsonTokenArrayEnd, 5, 6, 0, 0.0, ""]
    >>>)
    @assert = @example("[1,2,3]", <<<
      [JsonTokenArrayStart, 0, 1, 0, 0.0, ""]
      [JsonTokenNumberPre, 1, 1, 0, 0.0, ""]
      [JsonTokenNumber, 1, 2, 1, 0.0, ""]
      [JsonTokenNumberPre, 3, 3, 1, 0.0, ""]
      [JsonTokenNumber, 3, 4, 2, 0.0, ""]
      [JsonTokenNumberPre, 5, 5, 2, 0.0, ""]
      [JsonTokenNumber, 5, 6, 3, 0.0, ""]
      [JsonTokenArrayEnd, 6, 7, 3, 0.0, ""]
    >>>)
    @assert = @example("  [  1  ,  2  ,  3  ]  ", <<<
      [JsonTokenArrayStart, 2, 3, 0, 0.0, ""]
      [JsonTokenNumberPre, 5, 5, 0, 0.0, ""]
      [JsonTokenNumber, 5, 6, 1, 0.0, ""]
      [JsonTokenNumberPre, 11, 11, 1, 0.0, ""]
      [JsonTokenNumber, 11, 12, 2, 0.0, ""]
      [JsonTokenNumberPre, 17, 17, 2, 0.0, ""]
      [JsonTokenNumber, 17, 18, 3, 0.0, ""]
      [JsonTokenArrayEnd, 20, 21, 3, 0.0, ""]
    >>>)
    @assert = @example("[[[true]]]", <<<
      [JsonTokenArrayStart, 0, 1, 0, 0.0, ""]
      [JsonTokenArrayStart, 1, 2, 0, 0.0, ""]
      [JsonTokenArrayStart, 2, 3, 0, 0.0, ""]
      [JsonTokenTrue, 3, 7, 0, 0.0, ""]
      [JsonTokenArrayEnd, 7, 8, 0, 0.0, ""]
      [JsonTokenArrayEnd, 8, 9, 0, 0.0, ""]
      [JsonTokenArrayEnd, 9, 10, 0, 0.0, ""]
    >>>)

    @assert = @example_error("]]]",                   "")
    @assert = @example_error("[[[",                   "[[[")
    @assert = @example_error("[1,,,]",                "[1,")
    @assert = @example_error("[1,2,3}",               "[1,2,3")
    @assert = @example_error(<<<["fruit":"apple"]>>>, <<<["fruit">>>)

  :it "parses objects"
    @assert = @example("{}", <<<
      [JsonTokenObjectStart, 0, 1, 0, 0.0, ""]
      [JsonTokenObjectEnd, 1, 2, 0, 0.0, ""]
    >>>)
    @assert = @example("  {  }  ", <<<
      [JsonTokenObjectStart, 2, 3, 0, 0.0, ""]
      [JsonTokenObjectEnd, 5, 6, 0, 0.0, ""]
    >>>)
    @assert = @example(<<<{"fruit":"apple","edible":true}>>>, <<<
      [JsonTokenObjectStart, 0, 1, 0, 0.0, ""]
      [JsonTokenKeyPre, 2, 2, 0, 0.0, ""]
      [JsonTokenKey, 2, 7, 0, 0.0, "fruit"]
      [JsonTokenStringPre, 10, 10, 0, 0.0, "fruit"]
      [JsonTokenString, 10, 15, 0, 0.0, "apple"]
      [JsonTokenPairPost, 16, 16, 0, 0.0, "apple"]
      [JsonTokenKeyPre, 18, 18, 0, 0.0, "apple"]
      [JsonTokenKey, 18, 24, 0, 0.0, "edible"]
      [JsonTokenTrue, 26, 30, 0, 0.0, "edible"]
      [JsonTokenPairPost, 30, 30, 0, 0.0, "edible"]
      [JsonTokenObjectEnd, 30, 31, 0, 0.0, "edible"]
    >>>)
    @assert = @example(<<<  {  "a"  :  1  ,  "b"  :  2  }  >>>, <<<
      [JsonTokenObjectStart, 2, 3, 0, 0.0, ""]
      [JsonTokenKeyPre, 6, 6, 0, 0.0, ""]
      [JsonTokenKey, 6, 7, 0, 0.0, "a"]
      [JsonTokenNumberPre, 13, 13, 0, 0.0, "a"]
      [JsonTokenNumber, 13, 14, 1, 0.0, "a"]
      [JsonTokenPairPost, 16, 16, 1, 0.0, "a"]
      [JsonTokenKeyPre, 20, 20, 1, 0.0, "a"]
      [JsonTokenKey, 20, 21, 1, 0.0, "b"]
      [JsonTokenNumberPre, 27, 27, 1, 0.0, "b"]
      [JsonTokenNumber, 27, 28, 2, 0.0, "b"]
      [JsonTokenPairPost, 30, 30, 2, 0.0, "b"]
      [JsonTokenObjectEnd, 30, 31, 2, 0.0, "b"]
    >>>)
    @assert = @example(<<<{"t":[{"e":[{"s":[{"t":[]}]}]}]}>>>, <<<
      [JsonTokenObjectStart, 0, 1, 0, 0.0, ""]
      [JsonTokenKeyPre, 2, 2, 0, 0.0, ""]
      [JsonTokenKey, 2, 3, 0, 0.0, "t"]
      [JsonTokenArrayStart, 5, 6, 0, 0.0, "t"]
      [JsonTokenObjectStart, 6, 7, 0, 0.0, "t"]
      [JsonTokenKeyPre, 8, 8, 0, 0.0, "t"]
      [JsonTokenKey, 8, 9, 0, 0.0, "e"]
      [JsonTokenArrayStart, 11, 12, 0, 0.0, "e"]
      [JsonTokenObjectStart, 12, 13, 0, 0.0, "e"]
      [JsonTokenKeyPre, 14, 14, 0, 0.0, "e"]
      [JsonTokenKey, 14, 15, 0, 0.0, "s"]
      [JsonTokenArrayStart, 17, 18, 0, 0.0, "s"]
      [JsonTokenObjectStart, 18, 19, 0, 0.0, "s"]
      [JsonTokenKeyPre, 20, 20, 0, 0.0, "s"]
      [JsonTokenKey, 20, 21, 0, 0.0, "t"]
      [JsonTokenArrayStart, 23, 24, 0, 0.0, "t"]
      [JsonTokenArrayEnd, 24, 25, 0, 0.0, "t"]
      [JsonTokenPairPost, 25, 25, 0, 0.0, "t"]
      [JsonTokenObjectEnd, 25, 26, 0, 0.0, "t"]
      [JsonTokenArrayEnd, 26, 27, 0, 0.0, "t"]
      [JsonTokenPairPost, 27, 27, 0, 0.0, "t"]
      [JsonTokenObjectEnd, 27, 28, 0, 0.0, "t"]
      [JsonTokenArrayEnd, 28, 29, 0, 0.0, "t"]
      [JsonTokenPairPost, 29, 29, 0, 0.0, "t"]
      [JsonTokenObjectEnd, 29, 30, 0, 0.0, "t"]
      [JsonTokenArrayEnd, 30, 31, 0, 0.0, "t"]
      [JsonTokenPairPost, 31, 31, 0, 0.0, "t"]
      [JsonTokenObjectEnd, 31, 32, 0, 0.0, "t"]
    >>>)

    @assert = @example_error("}}}",                    "")
    @assert = @example_error("{{{",                    "{")
    @assert = @example_error(<<<{"a":1,,,"b":2}>>>,    <<<{"a":1,>>>)
    @assert = @example_error(<<<{"a":{"b":>>>,         <<<{"a":{"b":>>>)
    @assert = @example_error(<<<{"a":1,"b":2]>>>,      <<<{"a":1,"b":2>>>)
    @assert = @example_error(<<<{"apple","edible"}>>>, <<<{"apple">>>)

  :fun example (source String, expected String) Bool
    collector = _TestJsonParserCollector.new
    parser = JsonParser.new
    try (
      parser.parse!(source) -> (token | collector.collect(parser, token))
      actual = Inspect[collector]
      if (actual == expected) (
        True
      |
        @env.err.print("\n")
        @env.err.write("expected "), @env.err.write(Inspect[source])
        @env.err.print(" to parse as:"), @env.err.print(expected)
        @env.err.print("but actually parsed as:"), @env.err.print(actual)
        False
      )
    |
      @env.err.write("\nexpected "), @env.err.write(Inspect[source])
      @env.err.print(" to parse, but it failed to parse")
      False
    )

  :fun example_error (source String, valid_part String) Bool
    parser = JsonParser.new
    try (
      parser.parse!(source) -> (token | None)
      @env.err.write("\nexpected error parsing ")
      @env.err.write(Inspect[source])
      @env.err.print(" but it parsed successfully")
      False
    |
      actual = source.trim(0, parser.token_end.isize)
      if (valid_part == actual) (
        True
      |
        @env.err.write("\nexpected the valid part of ")
        @env.err.print(Inspect[source])
        @env.err.write("to equal:   ")
        @env.err.print(Inspect[valid_part])
        @env.err.write("but it was: ")
        @env.err.print(Inspect[actual])
        False
      )
    )

:class _TestJsonParserCollector
  :prop tokens Array(Array(
    (JsonToken | USize | USize | I64 | F64 | String | None)
  )): []

  :fun inspect_into (out String'iso) String'iso
    @tokens.each -> (item |
      out = Inspect.into(--out, item)
      out.push_byte('\n')
      None // TODO: this None shouldn't be needed
    )
    out.trim_in_place(0, -1) // remove the final newline
    --out

  :fun ref collect (parser JsonParser, token JsonToken) None
    @tokens << [
      token
      parser.token_start
      parser.token_end
      try (parser.last_number.as!(I64) | 0)
      try (parser.last_number.as!(F64) | 0)
      parser.last_string
    ]
