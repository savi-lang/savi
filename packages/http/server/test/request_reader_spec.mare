:import "spec"
:import "bytes" (ByteStreamChunkedReader)
:import ".."

:class RequestReaderSpec
  :is Spec
  :const describes: "RequestReader"

  :it "parses the head of an HTTP request"
    @assert = @example(["GET * HTTP/1.1\r\n\r\n"]) -> (req |
      @assert = req.method == "GET"
      @assert = req.uri_string == "*"
      @assert = req.content_length == 0
      @assert = req.transfer_encoding == TransferEncodingIdentity
      @assert = req.connection_header == ConnectionHeaderKeepAlive
    )

    @assert = @example([
      "POST /api/v1/entities HTTP/1.1\r\n"
      "cOnNeCtIoN: cLoSe\r\n"
      "CoNtEnT-lEnGtH: 123\r\n"
      "\r\n"
    ]) -> (req |
      @assert = req.method == "POST"
      @assert = req.uri_string == "/api/v1/entities"
      @assert = req.content_length == 123
      @assert = req.transfer_encoding == TransferEncodingIdentity
      @assert = req.connection_header == ConnectionHeaderClose
    )

    @assert = @example([
      "POST /api/v1/upload HTTP/1.1\r\n"
      "TrAnSfEr-EnCoDiNg: ChUnKeD\r\n"
      "\r\n"
    ]) -> (req |
      @assert = req.method == "POST"
      @assert = req.uri_string == "/api/v1/upload"
      @assert = req.content_length == 0
      @assert = req.transfer_encoding == TransferEncodingChunked
      @assert = req.connection_header == ConnectionHeaderKeepAlive
    )

  :it "complains when the request has an unsupported method"
    @assert = @example_error(
      ["GIMME * HTTP/1.1\r\n\r\n"]
      RequestErrorUnsupportedMethod
    )

  :it "complains when the request has a URI that is too long"
    uri_too_long = "_" * 8193 // just a whole lotta underscores
    @assert = @example_error(
      ["GET ", uri_too_long, " HTTP/1.1\r\n\r\n"]
      RequestErrorURITooLong
    )

  :it "complains when the request has an unsupported HTTP version"
    @assert = @example_error(
      ["GET * HTTP/9.9\r\n\r\n"]
      RequestErrorUnsupportedVersion
    )

  :it "complains when the request has an unsupported Transfer-Encoding header"
    @assert = @example_error(
      ["GET * HTTP/1.1\r\nTransfer-Encoding: bogus\r\n\r\n"]
      RequestErrorUnsupportedTransferEncoding
    )

  :it "complains when the request has an unsupported Connection header"
    @assert = @example_error(
      ["GET * HTTP/1.1\r\nConnection: bogus\r\n\r\n"]
      RequestErrorUnsupportedConnectionHeader
    )

  :: Parse the given source_chunks into a request and yield it to the caller.
  :: Returns True if everything went smoothly.
  :: Returns False and prints a message if something unexpected happened.
  :fun example (source_chunks Array(String)) Bool
    :yields Request
    reader = RequestReader.new
    stream = ByteStreamChunkedReader.new

    // Feed the data one byte at a time into the stream, which will
    // maximally test for parsing state problems at chunk boundaries.
    // Obviously this would be a bad choice in a real application,
    // but here we want to test the most pathological possible case.
    finished_too_soon = False
    saw_error = RequestErrorNone
    source_chunks.each -> (source_chunk |
      source_chunk.each_byte -> (byte |
        if finished_too_soon.not (
          try (
            reader.read!(stream)
            finished_too_soon = True // we don't expect to finish so soon here
          |
            // Check to see if there is an error present.
            // If so, store it and mark as having finished too soon.
            if (reader.last_error != RequestErrorNone) (
              saw_error = reader.last_error
              finished_too_soon = True
            )
          )
          stream << [byte]
        )
      )
    )

    if finished_too_soon (
      // If we finished too soon, there was either an error to show,
      // or the parser failed to consume all of the chunks before finishing.
      @env.err.write("\nexpected "), @env.err.write(Inspect[source_chunks])
      if (saw_error == RequestErrorNone) (
        @env.err.print(" to parse fully, but it finished too soon")
      |
        @env.err.write(" to not have an error, but it had error: ")
        @env.err.print(Inspect[saw_error])
      )
      False
    |
      // Now the stream has all the bytes and we expect it to actually work.
      try (
        yield reader.read!(stream)
        True
      |
        @env.err.write("\nexpected "), @env.err.write(Inspect[source_chunks])
        @env.err.print(" to parse, but it failed to parse")
        False
      )
    )

  :: Parse the given source_chunks into a request, expecting the given error.
  :: Returns True if the given error was indeed produced by the RequestReader.
  :: Returns False and prints a message if something unexpected happened.
  :fun example_error (
    source_chunks Array(String)
    expected_error RequestError
  ) Bool
    reader = RequestReader.new
    stream = ByteStreamChunkedReader.new

    // Feed the data one byte at a time into the stream, which will
    // maximally test for parsing state problems at chunk boundaries.
    // Obviously this would be a bad choice in a real application,
    // but here we want to test the most pathological possible case.
    finished_too_soon = False
    saw_error = RequestErrorNone
    source_chunks.each -> (source_chunk |
      source_chunk.each_byte -> (byte |
        if finished_too_soon.not (
          try (
            reader.read!(stream)
            finished_too_soon = True // we don't expect to finish so soon here
          |
            // Check to see if there is an error present.
            // If so, store it and mark as having finished too soon.
            if (reader.last_error != RequestErrorNone) (
              saw_error = reader.last_error
              finished_too_soon = True
            )
          )
          stream << [byte]
        )
      )
    )

    if (finished_too_soon) (
      // If we finished too soon, there was either an error to show,
      // or the parser failed to consume all of the chunks before finishing.
      case (
      | saw_error == RequestErrorNone |
        @env.err.write("\nexpected "), @env.err.write(Inspect[source_chunks])
        @env.err.print(" to parse fully, but it finished too soon")
        False
      | saw_error == expected_error |
        True
      |
        @env.err.write("\nexpected "), @env.err.write(Inspect[source_chunks])
        @env.err.write(" to have specific error: ")
        @env.err.write(Inspect[expected_error])
        @env.err.write(" but it actually had error: ")
        @env.err.print(Inspect[saw_error])
        False
      )
    |
      @env.err.write("\nexpected "), @env.err.write(Inspect[source_chunks])
      @env.err.print(" to have an error, but it finished successfully")
      False
    )
