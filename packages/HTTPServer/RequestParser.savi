:import "bytes" (ByteStreamReader)

:enum HTTPServer.RequestParser.Token
  :const bit_width U8: 8 // TODO: the compiler should choose the smallest valid bit width by default if not explicitly specified
  :member HTTPServer.RequestParser.Token.Method: 0
  :member HTTPServer.RequestParser.Token.Uri: 1
  :member HTTPServer.RequestParser.Token.VersionString: 2
  :member HTTPServer.RequestParser.Token.HeaderName: 3
  :member HTTPServer.RequestParser.Token.HeaderValue: 4

:enum _RequestAt
  :const bit_width U8: 8 // TODO: the compiler should choose the smallest valid bit width by default if not explicitly specified
  :member _RequestAtStart: 0
  :member _RequestAtMethod: 1
  :member _RequestAtUri: 2
  :member _RequestAtVersionString: 3
  :member _RequestAtHeaderLineMaybe: 4
  :member _RequestAtHeaderName: 5
  :member _RequestAtHeaderSpacing: 6
  :member _RequestAtHeaderValue: 7
  :member _RequestAtEnd: 8

:class HTTPServer.RequestParser
  :var _state _RequestAt: _RequestAtStart

  :fun ref reset: @_state = _RequestAtStart

  :fun ref parse!(stream ByteStreamReader)
    :yields HTTPServer.RequestParser.Token
    try (
      while (@_state != _RequestAtEnd) (
        @_state = case @_state == (
        | _RequestAtStart |
          _RequestAtMethod
        | _RequestAtMethod |
          stream.advance_while! -> (byte | byte != ' ')
          yield HTTPServer.RequestParser.Token.Method
          stream.advance!(1) // consume the space
          _RequestAtUri
        | _RequestAtUri |
          stream.advance_while! -> (byte | byte != ' ')
          yield HTTPServer.RequestParser.Token.Uri
          stream.advance!(1) // consume the space
          _RequestAtVersionString
        | _RequestAtVersionString |
          stream.advance_while! -> (byte | byte != '\r')
          if (stream.bytes_ahead < 2) error! // ensure the LF byte is ready too
          yield HTTPServer.RequestParser.Token.VersionString
          stream.advance!(2) // consume the CRLF
          _RequestAtHeaderLineMaybe
        | _RequestAtHeaderLineMaybe |
          if (stream.peek_byte! == '\r' && stream.peek_byte!(1) == '\n') (
            stream.advance!(2)
            _RequestAtEnd
          |
            _RequestAtHeaderName
          )
        | _RequestAtHeaderName |
          stream.advance_while! -> (byte | byte != ':')
          yield HTTPServer.RequestParser.Token.HeaderName
          stream.advance!(1) // consume the colon
          _RequestAtHeaderSpacing
        | _RequestAtHeaderSpacing |
          stream.advance_while! -> (byte | byte == ' ' || byte == '\t')
          _RequestAtHeaderValue
        | _RequestAtHeaderValue |
          stream.advance_while! -> (byte | byte != '\r')
          if (stream.bytes_ahead < 2) error! // ensure the LF byte is ready too
          yield HTTPServer.RequestParser.Token.HeaderValue
          stream.advance!(2) // consume the CRLF
          _RequestAtHeaderLineMaybe
        |
          // TODO: Refactor as exhaustive match to avoid unreachable else case
          _RequestAtStart
        )
        stream.mark_here
      )
    |
      // Rewind to the mark, in case we only got partway through a token.
      // We will pick up from here at the mark when we get a new data chunk.
      stream.rewind_to_marker
      error!
    )
