:import "./pattern"

:enum Op
  :const bit_width U8: 8

  :fun non from!(byte U8): @from_u64!(byte.u64)

  // We reserve everything below the minimum opcode to match the specified byte.
  :fun non min_op U8: 0xF3

  // This could theoretically be used for any byte, but we only use it for
  // bytes from min_op and higher; all lower op codes indicate the byte itself.
  :member OpByte: 0xF3

  :member OpMatch: 0xF4
  :member OpRecursiveMatch: 0xF5 // For lookahead
  :member OpJump: 0xF6
  :member OpSplit: 0xF7
  :member OpSplitMany: 0xF8
  :member OpAnyByte: 0xF9
  :member OpByteRange: 0xFA
  :member OpSaveStart: 0xFB
  :member OpSaveFinish: 0xFC
  :member OpBackrefCompare: 0xFD
  :member OpInlineZWA: 0xFE
  :member OpRecursiveZWA: 0xFF

:class _ProgramWriter
  :let program: Bytes.new

  :fun _cursor: @program.size.u32

  :fun ref _op(op Op): @program.push(op.u8)
  :fun ref _byte(byte U8): @program.push(byte)
  :fun ref _u32(value U32): @program.push_native_u32(value)
  :fun ref _addr(addr U32): @program.push_native_u32(addr)
  :fun ref _later_addr: cursor = @_cursor, @_addr(-1), cursor
  :fun ref _store_current_cursor_at!(store_addr U32)
    @program.write_native_u32!(store_addr.usize, @_cursor)

  :fun ref compile(pattern Pattern)
    @_pattern(pattern)
    @_op(OpMatch)
    @

  :fun ref _pattern(pattern Pattern)
    case pattern <: (
    | PatternAnyByte |
      @_op(OpAnyByte)

    | PatternByte |
      @_op(OpByte)
      @_byte(pattern.byte)

    | PatternByteRange |
      @_op(OpByteRange)
      @_byte(pattern.low)
      @_byte(pattern.high)

    | PatternCharacter |
      pattern.each_byte -> (byte |
        if (byte < Op.min_op) (
          @_byte(byte)
        |
          @_op(OpByte)
          @_byte(byte)
        )
      )

    | PatternSequence |
      pattern.children.each -> (child | @_pattern(child))

    | PatternOptional |
      @_op(OpSplit)
      primary_addr = @_later_addr
      secondary_addr = @_later_addr

      present_addr = if pattern.is_non_greedy (secondary_addr | primary_addr)
      absent_addr = if pattern.is_non_greedy (primary_addr | secondary_addr)

      try @_store_current_cursor_at!(present_addr)
      @_pattern(pattern.child)

      try @_store_current_cursor_at!(absent_addr)

    | PatternZeroOrMore |
      start_cursor = @_cursor
      @_op(OpSplit)
      primary_addr = @_later_addr
      secondary_addr = @_later_addr

      present_addr = if pattern.is_non_greedy (secondary_addr | primary_addr)
      absent_addr = if pattern.is_non_greedy (primary_addr | secondary_addr)

      try @_store_current_cursor_at!(present_addr)
      @_pattern(pattern.child)

      @_op(OpJump)
      @_addr(start_cursor)

      try @_store_current_cursor_at!(absent_addr)

    | PatternOneOrMore |
      start_cursor = @_cursor
      @_pattern(pattern.child)

      @_op(OpSplit)
      if pattern.is_non_greedy (
        after_addr = @_later_addr
        @_addr(start_cursor)
        try @_store_current_cursor_at!(after_addr)
      |
        @_addr(start_cursor)
        after_addr = @_later_addr
        try @_store_current_cursor_at!(after_addr)
      )

    | PatternChoice |
      if (pattern.children.size == 2) (
        @_op(OpSplit)
        primary_addr = @_later_addr
        secondary_addr = @_later_addr

        try @_store_current_cursor_at!(primary_addr)
        try @_pattern(pattern.children[0]!)

        @_op(OpJump)
        after_addr = @_later_addr

        try @_store_current_cursor_at!(secondary_addr)
        try @_pattern(pattern.children[1]!)

        try @_store_current_cursor_at!(after_addr)
      |
        @_op(OpSplitMany)
        branch_addrs Array(U32) = []
        pattern.children.each -> (child | branch_addrs << @_later_addr)
        @_addr(0) // branch list terminator

        after_addrs Array(U32) = []
        pattern.children.each_with_index -> (child, index |
          if (index > 0) (
            @_op(OpJump)
            after_addrs << @_later_addr
          )
          try @_store_current_cursor_at!(branch_addrs[index]!)
          @_pattern(child)
        )

        after_addrs.each -> (after_addr |
          try @_store_current_cursor_at!(after_addr)
        )
      )
    )

    @
